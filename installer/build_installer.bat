@echo off
cd packages\com.forze.h2analyst

if not exist data\ (
    mkdir data
    goto build
) else (
    goto clean-data
)

:clean-data
set /p _var="This will remove the current installer. Do you want to continue? (Y/[N]): "
if /i not "%_var%"=="Y" goto end
echo Removing existing installer data..
del data\*.* /S /Q 1>nul
if exist ..\..\H2AnalystInstaller.exe (
    del ..\..\H2AnalystInstaller.exe 1>nul
)


:build
echo Building new installer..
:: Place .exe generated by release build in installer data folder
set _buildPath="..\..\..\build\x64\Release\H2Analyst.exe"
if not exist %_buildPath% (
    echo Cannot find H2Analyst.exe in release build folder
    goto end
)
copy "..\..\..\build\x64\Release\H2Analyst.exe" "data\" 1>nul
:: Use QtInstall env var to find the required Qt Tools
if not defined QtInstall (
    echo Define QtInstall environment variable
    goto end
)
:: Create the installer in this folder
cd data
%QtInstall%\6.5.3\msvc2019_64\bin\windeployqt.exe H2Analyst.exe --compiler-runtime --no-translations
:: Move VC++ redist to a seperate package folder
cd ..\..
if not exist com.microsoft.vcredist\data\ (
    mkdir com.microsoft.vcredist\data
) else (
    del com.microsoft.vcredist\data\*.* /S /Q 1>nul
)
move "com.forze.h2analyst\data\vc_redist.x64.exe" "com.microsoft.vcredist\data\" 1>nul
cd ..
%QtInstall%\Tools\QtInstallerFramework\4.7\bin\binarycreator.exe -c .\config\config.xml -p packages -f H2AnalystInstaller
echo Installer has been created successfully

:end
echo.
echo Script has finished. Press any key to exit...
pause >nul